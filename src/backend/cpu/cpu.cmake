set(HEADERS_BACKEND_CPU
    src/backend/cpu/Cpu.h
    src/backend/cpu/CpuBackend.h
    src/backend/cpu/CpuConfig_gen.h
    src/backend/cpu/CpuConfig.h
    src/backend/cpu/CpuLaunchData.cpp
    src/backend/cpu/CpuThread.h
    src/backend/cpu/CpuThreads.h
    src/backend/cpu/CpuWorker.h
    src/backend/cpu/interfaces/ICpuInfo.h
    src/backend/cpu/platform/BasicCpuInfo.h
   )

set(SOURCES_BACKEND_CPU
    src/backend/cpu/Cpu.cpp
    src/backend/cpu/CpuBackend.cpp
    src/backend/cpu/CpuConfig.cpp
    src/backend/cpu/CpuLaunchData.h
    src/backend/cpu/CpuThread.cpp
    src/backend/cpu/CpuThreads.cpp
    src/backend/cpu/CpuWorker.cpp
   )

if (WITH_HWLOC)
    find_package(HWLOC REQUIRED)
    include_directories(${HWLOC_INCLUDE_DIR})
    set(CPUID_LIB ${HWLOC_LIBRARY})

    add_definitions(/DRXS_FEATURE_HWLOC)

    if (HWLOC_DEBUG)
        add_definitions(/DRXS_HWLOC_DEBUG)
    endif()

    list(APPEND HEADERS_BACKEND_CPU src/backend/cpu/platform/HwlocCpuInfo.h)
    list(APPEND SOURCES_BACKEND_CPU src/backend/cpu/platform/HwlocCpuInfo.cpp)
else()
    remove_definitions(/DRXS_FEATURE_HWLOC)

    set(CPUID_LIB "")
endif()

if (RXS_RISCV)
    list(APPEND SOURCES_BACKEND_CPU
        src/backend/cpu/platform/lscpu_riscv.cpp
        src/backend/cpu/platform/BasicCpuInfo_riscv.cpp
    )
elseif (RXS_ARM)
    list(APPEND SOURCES_BACKEND_CPU src/backend/cpu/platform/BasicCpuInfo_arm.cpp)

    if (RXS_OS_APPLE)
        list(APPEND SOURCES_BACKEND_CPU src/backend/cpu/platform/BasicCpuInfo_arm_mac.cpp)
    else()
        list(APPEND SOURCES_BACKEND_CPU
            src/backend/cpu/platform/lscpu_arm.cpp
            src/backend/cpu/platform/BasicCpuInfo_arm_unix.cpp
        )
    endif()
else()
    list(APPEND SOURCES_BACKEND_CPU src/backend/cpu/platform/BasicCpuInfo.cpp)
endif()
